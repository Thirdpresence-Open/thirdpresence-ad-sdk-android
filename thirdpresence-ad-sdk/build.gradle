apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.thirdpresence.adsdk.sdk'
version = rootProject.ext.versionString

def siteUrl = 'https://github.com/Thirdpresence-Open/thirdpresence-ad-sdk-android'
def gitUrl = 'https://github.com/Thirdpresence-Open/thirdpresence-ad-sdk-android.git'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    useLibrary  'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode rootProject.ext.buildNum
        versionName rootProject.ext.versionString
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

configurations {
    javadocDeps
}

repositories {
    google()
    jcenter()
    flatDir{
        dirs 'libs'
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api fileTree(include: ['*.jar'], dir: 'moat/libs')
    testImplementation 'junit:junit:4.12'
    implementation "com.android.support:support-compat:${rootProject.ext.supportLibVersion}"
    implementation "com.android.support:support-core-utils:${rootProject.ext.supportLibVersion}"
    implementation "com.android.support:support-annotations:${rootProject.ext.supportLibVersion}"
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    pkg {
        repo = 'thirdpresence-ad-sdk-android'
        name = 'com.thirdpresence.adsdk.sdk'
        userOrg = "thirdpresence"

        version {
            name = version
            released  = new Date()
            vcsTag = version
        }

        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        websiteUrl = siteUrl
        publish = true
    }
    configurations = ['archives']
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'Thirdpresence Ad SDK for Android'
                url siteUrl

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'maokkone'
                        name 'Marko Okkonen'
                        email 'marko.okkonen@thirdpresence.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task writeVersionNumber {
    doLast {
        def buildDir = new File("$rootDir/build/")
        buildDir.mkdirs()

        def versionFile = new File("$rootDir/build/sdk_version.log")
        versionFile.createNewFile()
        versionFile.write(version)
    }
}
build.dependsOn(writeVersionNumber)

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc, dependsOn: sourcesJar) {
    source = android.sourceSets.main.java.srcDirs
    classpath = configurations.compile
    include("**/VideoAd.java")
    include("**/VideoAdManager.java")
    include("**/VideoInterstitial.java")
    include("**/RewardedVideo.java")
    include("**/BannerView.java")
    include("**/VideoBanner.java")
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task copyLibs(type: Copy) {
    from configurations.compile
    into 'libs'
}